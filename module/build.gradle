plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 31
        versionName "2.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // @namgyupark (2021-09-25) : change aar name when it builds
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
//                outputFileName = "${archivesBaseName}-${android.defaultConfig.versionName}-${variant.buildType.name}.aar"
                outputFileName = "curl-generator-${android.defaultConfig.versionName}.aar"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // @namgyupark (2021-09-25) : FileNotFoundException when trying to read assets with Roboelectric
    // (https://stackoverflow.com/questions/44249462/filenotfoundexception-when-trying-to-read-assets-with-roboelectric)
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // dependency library
    implementation "com.squareup.okhttp3:okhttp:4.9.1"

    // test library
    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.mockito:mockito-core:3.8.0"     // Mock dependent libraries object
    testImplementation 'org.mockito:mockito-inline:3.8.0'  // Mockito cannot mock/spy because : - final class
    testImplementation "org.robolectric:robolectric:4.5.1"    // A library which contains many mocks of Android classes. (No need Android emulator or Device)
}
